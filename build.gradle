buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.jenkins-ci.org/releases/'
        }
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.12.2'
        classpath 'org.ajoberstar:gradle-git:1.3.2'
    }
}

apply plugin: 'org.jenkins-ci.jpi'
apply plugin: 'groovy'


group = "org.jenkins-ci.plugins"
version = "0.2.0"
description = "Matrix project axis creation and exclusion plugin using yaml file"

jenkinsPlugin {
    coreVersion = '1.609'
    displayName = 'Yaml Axis plugin'
    url = 'https://wiki.jenkins-ci.org/display/JENKINS/Yaml+Axis+Plugin'
    gitHubUrl = 'https://github.com/jenkinsci/yaml-axis-plugin'
    shortName = 'yaml-axis'

    // The developers section is optional, and corresponds to the POM developers section.
    developers {
        developer {
            id 'sue445'
            name 'sue445'
            email 'sue445@sue445.net'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

repositories {
    mavenCentral()
}

dependencies {
    // NOTE: groovy version included in Jenkins is 1.8.9
    runtime 'org.codehaus.groovy:groovy-all:1.8.9'
    compile 'org.yaml:snakeyaml:1.16'

    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'

    jenkinsPlugins 'org.jenkins-ci.plugins:matrix-project:1.6@jar'
}

test {
    testLogging {
        showStandardStreams = true
    }
}

import org.ajoberstar.grgit.*

ext.repo = Grgit.open()

task tagRelease << {
    repo.tag.add {
        name = version
        message = "Release of ${version}"
    }

    repo.push(tags: true)
}

task checkVersion << {
    if(version.endsWith("-SNAPSHOT")){
        throw new GradleException("Remove '-SNAPSHOT' in version!")
    }
}

task release(dependsOn: ["checkVersion", "publish", "tagRelease"])
